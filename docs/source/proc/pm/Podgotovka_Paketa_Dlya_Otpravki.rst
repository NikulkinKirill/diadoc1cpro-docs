
Как подготовить пакет документов для отправки
=============================================

Одним из основных объектов работы модуля является :doc:`Пакет <../../objects/Paket>` документов. Состоять такой объект может из различных :doc:`типов документов <../../objects/Tipy_Dokumentov>`.

Аналогично веб-версии, одновременно нельзя отправить более 30 документов.

Для формирования и отправки документов в одном пакете, следует выполнить следующие действия:

**1.** Определить пакет в функции :doc:`ПолучитьТаблицуИспользуемыхПакетов <../../func/pm/Poluchit'TablitsuIspol'zuyemykhPaketov>`.

Пример определения пакета "УПД" и пакета "СФ + ТОРГ12/Акт":

::

      ТЗ = Новый ТаблицаЗначений;
      ТЗ.Колонки.Добавить("ID");
      ТЗ.Колонки.Добавить("Наименование");
      ТЗ.Колонки.Добавить("УПД");

      ДобавитьСтрокуВТаблицуЗначений(ТЗ, "ID_УПД", "УПД", Истина);
      ДобавитьСтрокуВТаблицуЗначений(ТЗ, "ID_ОсновнойПакет", "СФ + ТОРГ12/Акт");

**2.** Определить :doc:`типы используемых документов <../../objects/Tipy_Dokumentov>` в функции :doc:`ПолучитьТаблицуИспользуемыхВидовДокументов <../../func/pm/Poluchit'TablitsuIspol'zuyemykhVidovDokumentov>`

Пример определения типов документов: УПД (формализованный файл в формате ФНС) и Протокол согласования цен (неформализованный pdf файл):

::

      ТЗ = Новый ТаблицаЗначений;
      ТЗ.Колонки.Добавить("ID");
      ТЗ.Колонки.Добавить("Наименование");
      ТЗ.Колонки.Добавить("ТипДокумента");

      ДобавитьСтрокуВТаблицуЗначений(ТЗ, "ID_УПД", "УПД", "UniversalTransferDocument");
      ДобавитьСтрокуВТаблицуЗначений(ТЗ, "ID_ПротоколСогласованияЦен", "Протокол согласования цен", "Nonformalized"); // внешняя ПФ

**3.** Определить список документов 1С, на основании которых будут сформированы пакеты на отправку требуемого вида пакета в функции :doc:`ПолучитьТекстЗапросаДляСпискаПакетовНаОтправкуПоМассивуВидовПакетов <../../func/pm/Poluchit'TekstZaprosaDlyaSpiskaPaketovNaOtpravkuPoMassivuVidovPaketov>`

Пример части запроса для выбора счетов-фактур из 1С, на основании которых будут сформированы пакеты вида "УПД":

::

      Если ВидПакетаРазвернутый.ID = "ID_УПД" Тогда

          Результат = ДобавитьОбъединениеВТекстЗапроса(Результат) +

          "ВЫБРАТЬ
          | СчетФактураВыданный.Ссылка КАК Документ,
          | СчетФактураВыданный.Номер КАК НомерДокумента,
          | СчетФактураВыданный.Дата КАК ДатаДокумента,
          | СчетФактураВыданный.Контрагент КАК Контрагент,
          | СчетФактураВыданный.Организация КАК Организация,
          | СчетФактураВыданный.СуммаДокумента КАК СуммаДокумента,
          | ""ID_УПД"" КАК ВидПакетаID
          |ИЗ
          | Документ.СчетФактураВыданный КАК СчетФактураВыданный
          |     ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_КэшКонтрагентовПоВидамУПД КАК ВТ_КэшКонтрагентовПоВидамУПД
          |     ПО СчетФактураВыданный.Организация = ВТ_КэшКонтрагентовПоВидамУПД.Организация1С
          |         И СчетФактураВыданный.Контрагент = ВТ_КэшКонтрагентовПоВидамУПД.Контрагент1С
          |     ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПакетЭД.ЭлектронныеДокументы КАК ОтправленныеДокументы
          |     ПО ТипЗначения(ОтправленныеДокументы.ОбъектВладелец) = Тип(Документ.СчетФактураВыданный)
          |         И СчетФактураВыданный.Ссылка = ОтправленныеДокументы.ОбъектВладелец
          |         И (ОтправленныеДокументы.Ссылка.ВнешнийУИД = ""ID_УПД"")
          |ГДЕ
          | ОтправленныеДокументы.Ссылка ЕСТЬ NULL
          | И СчетФактураВыданный.Дата МЕЖДУ &НачалоПериода И &КонецПериода
          | И СчетФактураВыданный.Проведен
          | И СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаРеализацию)
          | И СчетФактураВыданный.ДоговорКонтрагента.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПокупателем)
          | И НЕ СчетФактураВыданный.Исправление
          | И ВТ_КэшКонтрагентовПоВидамУПД.ИспользоватьУПД_МеткаОсновногоПакета";

      КонецЕсли;

**4.** Определить состав пакета в функции :doc:`ПодготовитьПакет <../../func/pm/Podgotovit'Paket>`

Пример определения состава пакета вида "УПД" (формируется из одного документа Счет-фактура) и пакета вида "СФ + ТОРГ12/Акт" (состоит из документа Счет-фактура и его документов-оснований - Акт или ТОРГ-12):

::

      Если ВидПакетаРазвернутый.ID = "ID_УПД" Тогда

          ОсновнойМодуль.ЭДО_ДокументМенеджер_ПодготовитьИДобавитьДокументВПакет(Пакет, СтрокаСписка.Документ, "ID_УПД");

      ИначеЕсли ВидПакетаРазвернутый.ID = "ID_ОсновнойПакет" Тогда

          ОсновнойМодуль.ЭДО_ДокументМенеджер_ПодготовитьИДобавитьДокументВПакет(Пакет, СтрокаСписка.Документ, "ID_СчетФактура");

          Для Каждого СтрокаТЧ Из СтрокаСписка.Документ.ДокументыОснования Цикл

            Если ТипЗнч(СтрокаТЧ.ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг") И СтрокаТЧ.ДокументОснование.Товары.Количество() > 0 Тогда
                ОсновнойМодуль.ЭДО_ДокументМенеджер_ПодготовитьИДобавитьДокументВПакет(Пакет, СтрокаТЧ.ДокументОснование, "ID_ТОРГ12");
            Иначе
                ОсновнойМодуль.ЭДО_ДокументМенеджер_ПодготовитьИДобавитьДокументВПакет(Пакет, СтрокаТЧ.ДокументОснование, "ID_АктФормализованный");
            КонецЕсли;

          КонецЦикла;

      КонецЕсли;

**5.** Собрать контент для каждого типа документа пакета в функции :doc:`ПодготовитьЭлектронныйДокумент <../../func/pm/Podgotovit'ElektronnyyDokument>`

Пример сбора контента для документов типа "УПД":

::

      Документ1С               = Результат.Документ1С;
      ВидДокументаНаименование = ВидДокументаРазвернутый.Наименование;
      ТипКонтента              = ВидДокументаРазвернутый.ТипКонтента;
      ID                       = ВидДокументаРазвернутый.ID;

      Если ВРЕГ(ТипКонтента) = ВРЕГ("UtdSellerContent") Тогда

          ДопСведения   = Неопределено;
          ФИОПодписанта = " ";

          //Получим тип документа УПД, который используется в типовом модуле
          Если ID = "ID_УПД" Тогда
              ФункцияУПД = "СЧФДОП";
          ИначеЕсли ID = "ID_УПД_СЧФ" ИЛИ ID = "ID_СчетФактура" ИЛИ ID = "ID_ИСФ" ИЛИ ID = "ID_ИУПД" Тогда
              ФункцияУПД = "СЧФ";
          ИначеЕсли ID = "ID_УПД_ДОП" ИЛИ ID = "ID_АктФормализованный" ИЛИ ID = "ID_ТОРГ12" Тогда
              ФункцияУПД = "ДОП";
          Иначе
              ВызватьИсключение "Неизвестный тип документа: " + ВидДокументаНаименование;
          КонецЕсли;

          ДополнительныеПараметры = Новый Структура("ПараметрыСогласования, ДопСведения, ФИОПодписанта, ФункцияУПД", Неопределено, ДопСведения, ФИОПодписанта, ФункцияУПД);
          Content = ОсновнойМодуль.ТиповойМодуль_ПолучитьКонтент(ТиповойМодульДиадока(), РежимУправляемыхФорм, Документ1С, ТипКонтента, ДополнительныеПараметры);

          ОсновнойМодуль.ЗаполнитьКонтентXDTOПоСтруктуре(Результат.Content, Content);

      КонецЕсли;
